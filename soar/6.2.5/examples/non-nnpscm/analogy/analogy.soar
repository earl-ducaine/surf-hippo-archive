;;; Program  analogy.soar, non-nnpscm
;;; Started  5.8.93
;;; Version  1.11.94

;;; Program to do simple 'deliberate analogy' for figuring out how to launch
;;; a program on the Mac, given that one or two specific ones are known.

;;; This version developed specifically for the purposes of the one-day
;;; Soar tutorial given by Frank Ritter and Richard Young.  A set of
;;; Macintosh files containing OHP slides together with exercises and
;;; other handouts (either describing just this program, else covering
;;; the whole one-day tutorial) can be obtained by contacting
;;;	ritter@psyc.nott.ac.uk

;;; Note that I have used state-elaboration rules instead of operators in many
;;; places, probably to a far greater extent than is justified in terms of
;;; cognitive modelling, but it shortens the program and perhaps makes its
;;; behaviour easier to follow.

;;; Correspondingly, there are several highly specific monitoring rules present,
;;; which we would not normally expect to see in this kind of program.


;;; ======================================================================
;;; DECLARATIONS, INITIALISATIONS, ETC.
;;; ======================================================================


;;; This format is specifically for monitoring the Imagine-Task space

(object-trace-format :add p perform
	"%id (perform%ifdef[ %v[type] %v[alias]])")


;;; Ask what program to launch

(sp preamble*ask-program-to-launch
  (goal <g> ^name perform ^state <s>)
  (<s> ^task <t>)
	(<t> ^feature <f2>)
	(<f2> ^fname arg1)
  -->
  (write (crlf) |What program to launch? [cg,word,draw,xl] |)
  (<f2> ^fvalue (accept)))


;;; ======================================================================
;;; PERFORM SPACE
;;; ======================================================================

;;; -- Initialisation --

(sp perform*initialise*gps
  (goal <g> ^object nil)
  -->
  (<p> ^name perform)
  (<g> ^name perform ^problem-space <p> ^state <s>))

(sp perform*initialise*state
  (goal <g> ^name perform ^state <s>)
  -->
  (<s> ^task <t>)
	(<t> ^feature <f1> + <f1> &, <f2> + <f2> &)
	(<f1> ^fname effect ^fvalue launch)
	(<f2> ^fname arg1))


;;; -- Termination --

;;; Kludgy-detect when a program has been successfully launched

(sp perform*task*terminate
  (goal <g> ^problem-space.name perform ^state <s>)
  (<s> ^task <t>)
	(<t> ^feature <f1> <f2>)
	(<f1> ^fname effect ^fvalue launch)
	(<f2> ^fname arg1   ^fvalue <prog>)
  (<s> ^simulation <sim>)
  (<sim> ^status running ^object <prog>)
  -->
  (write (crlf) |** Yeah, the program is running!|)
  (halt))


;;; -- Performing actions --

;;; If there's ever an Action attribute on the state, then perform the
;;; action.

;;; Issues about how to know when an action has been completed,
;;; what happens if there is more than one action proposed, and so on,
;;; are beyond the scope of this demonstation program (and hopefully will
;;; not arise).

;;; Propose a perform operator

(sp perform*perform*propose
  (goal <g> ^problem-space.name perform ^state <s>)
  (<s> ^action <a>)
  -->
  (<o> ^name perform ^action <a>)
  (<g> ^operator <o>))


;;; Performing the double-click action

(sp perform*perform*double-click*perform
  (goal <g> ^problem-space.name perform ^state <s> ^operator <o>)
  (<o> ^name perform ^action <a>)
  (<a> ^movement double-click ^object <obj>)
  -->
  (<s> ^motor <m>)
  (<m> ^movement double-click ^object <obj>)
  (write (crlf) |** User double-clicks on object | <obj>))


;;; -- Kludgy simulation of Mac launching programs --

;;; The following rule would be more elegant if it knew what objects
;;; were 'isa' program

(sp perform*simulate*launch*program
  (goal <g> ^problem-space.name perform ^state <s> ^operator.name <oname>)
  (<s> ^motor <m>)
  (<m> ^movement double-click ^object <obj> ^object << CG Word Draw XL >> )
  -->
  (<s> ^simulation <sim>)
  (<sim> ^status running ^object <obj>)
  (write (crlf) |** Mac now has program | <obj> | running|))


;;; -- Just for testing --

;;; Just for testing, the following hand-written chunk proposes the action
;;; of double-clicking on a program to be launched.  Commented out.

#|
(sp perform*task*action*double-click-to-launch
  (goal <g> ^problem-space.name perform ^state <s>)
  (<s> ^task <t>)
	(<t> ^feature <f1> <f2>)
	(<f1> ^fname effect ^fvalue launch)
	(<f2> ^fname arg1   ^fvalue <prog>)
  -->
  (<s> ^action <a>)
  (<a> ^movement double-click ^object <prog>))
|#


;;; Monitor ^task and ^action on the state

(sp perform*monitor*state*task
  (goal <g> ^problem-space.name perform ^state <s>)
  (<s> ^task <t>)
	(<t> ^feature <f1> {<> <f1> <f2>})
	(<f1> ^fname effect ^fvalue <effect>)
	(<f2> ^fname arg1   ^fvalue <prog>)
  -->
  (write (crlf) |State has ^task: effect=| <effect> | arg1=| <prog>))

(sp perform*monitor*state*action
  (goal <g> ^problem-space.name perform ^state <s>)
  (<s> ^action <a>)
	(<a> ^movement <move> ^object <obj>)
  -->
  (write (crlf) |State has ^action: | <move> | | <obj>))


;;; ======================================================================
;;; ACTION-PROPOSAL SPACE
;;; ======================================================================

;;; This is where we drop down to when no action is proposed in the Perform
;;; space for a given task.  The impasse shows up as a State-No-Change.

;;; -- Initialise --

;;; Check that there's a task but no action  ... Oh no!!  That's going to
;;; give rise to self-undermining chunks.  For now, check just that there's
;;; a task, we'll assume no action.

(sp action-proposal*initialise
  (goal <g> ^object <sg> ^impasse no-change ^attribute state)
  (<sg> ^problem-space.name perform ^state <ss>)
  (<ss> ^task <t>)
  -->
  (<g> ^problem-space <p> ^state <s>)
  (<p> ^name action-proposal)
  (<s> ^superstate <ss>))


;;; -- Assumed knowledge --

;;; Here we hand-code the assumed knowledge about how to launch Word or Draw
;;; by double-clicking.  When used, this will give rise to specific chunks
;;; back in the Perform space.

;;; If the task is to launch Word or Draw, then propose an action to
;;; double-click.

(sp action-proposal*double-click-to-launch
  (goal <g> ^problem-space.name action-proposal ^state.superstate <ss>)
  (<ss> ^task <t>)
	(<t> ^feature <f1> <f2>)
	(<f1> ^fname effect ^fvalue launch)
	(<f2> ^fname arg1   ^fvalue <prog> ^fvalue << Word Draw >> )
  -->
  (<ss> ^action <a>)
  (<a> ^movement double-click ^object <prog>))


;;; -- Use analogy --

;;; The only "method" we have is to try using analogy.  We'll do this by
;;; proposing the use-analogy operator, which will implement in its own space.

(sp action-proposal*use-analogy*propose
  (goal <g> ^problem-space.name action-proposal)
  -->
  (<o> ^name use-analogy)
  (<g> ^operator <o>))


;;; ======================================================================
;;; USE-ANALOGY SPACE
;;; ======================================================================

;;; This space arise from an Operator No-Change below the Use-analogy
;;; operator.  The logic embodied in this space is as follows:
;;;
;;; (1) If the task involves some Effect on some Object Y
;;; (2) where the Object is of a known Class
;;; (3) and we can recall other members X of that Class
;;; (4) then we imagine the task of achieving that Effect on object X
;;; (5) we replace X by a *general descriptor* of Y
;;; (6) and return that as the recommended action.


;;; -- Initialise --

(sp use-analogy*initialise
  (goal <g> ^object <sg> ^impasse no-change ^attribute operator)
  (<sg> ^operator.name use-analogy ^state.superstate <sss>)
  -->
  (<g> ^problem-space <p> ^state <s>)
  (<p> ^name use-analogy)
  (<s> ^task-state <sss>))


;;; -- The method --

;;; Recognise the applicability of the method (= step 1)

(sp use-analogy*analogy-1*recognise
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^task-state.task <t>)
	(<t> ^feature <f1> <f2>)
	(<f1> ^fname effect ^fvalue <effect>)
	(<f2> ^fname arg1   ^fvalue <obj>)
  -->
  (<s> ^analogy-method analogy-1))


;;; Attempt to recall other members of the same object class (= (2) & (3)).
;;; Note that this part is being handled very crudely.  Since we don't have
;;; a justified representation of categorial knowledge, we're simply going
;;; to map directly from program -> Word and Draw.

;;; I ought properly to be doing this with operators (for recall, etc.),
;;; but since this part is all monotonic and elaborative, I'm using plain
;;; old elaborations.

;;; Get the class of the object (= step 2)  (NB specialised for CG or XL)
;;; NB Notice how the first two clauses in this and subsequent rules are
;;;	separated off, to give a standard beginning saying "Hey! We're
;;;	applying the analogy-1 method".

(sp use-analogy*analogy-1*get-object-class*CG-XL
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^analogy-method analogy-1)
  ;
  (<s> ^task-state.task <t>)
	(<t> ^feature <f2>)
	(<f2> ^fname arg1   ^fvalue << CG XL >> )
  -->
  (<s> ^object-class program))


;;; Recall other members of that class [program] (= step 3)

(sp use-analogy*analogy-1*recall-members*program
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^analogy-method analogy-1)
  ;
  (<s> ^object-class program)
  -->
  (<s> ^class-member Word + Word =, Draw + Draw =, XL + XL =))
				; Note that the items are made indifferent


;;; -- Imagining the task -- (= step 4)

;;; Now the fun starts.  For a given ^class-member, we imagine the task
;;; which is the given task applied to the class-member.  We do this via
;;; an operator, because it is going to be important to do the actual
;;; imagining in a subgoal which closely resembles the top context.

(sp use-analogy*analogy-1*imagine-task*propose
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^analogy-method analogy-1)
  ;
  (<s> ^object-class <class> ^class-member <member> -^imagined-action)
  (<s> ^task-state.task <t>)
	(<t> ^feature <f1>)
	(<f1> ^fname effect ^fvalue <effect>)
  -->
  (<o> ^name imagine-task ^task <imag-t>)
	(<imag-t> ^feature <f1> + <f1> &, <if2> + <if2> &)
	(<if2> ^fname arg1 ^fvalue <member>)
  (<g> ^operator <o>))

;;; Monitor the imagine-task operator

(sp use-analogy*imagine-task*monitor
  (goal <g> ^problem-space.name use-analogy ^operator <o>)
  (<o> ^name imagine-task ^task <imag-t>)
	(<imag-t> ^feature <f1> <f2>)
	(<f1> ^fname effect ^fvalue <effect>)
	(<f2> ^fname arg1 ^fvalue <member>)
  -->
  (write | [| <effect> | | <member> |]|))


;;; The imagine-task operator is of course implemented in its own space
;;; (see below).  When it "returns" it will *either* have an ^action marked
;;; on it, or else a ^fail.  We terminate the operator in either case, with
;;; appropriate responses:

;;; For a ^fail, reject the class-member and terminate the operator

(sp use-analogy*analogy-1*imagine-task*fail*reject-member
  (goal <g> ^problem-space.name use-analogy ^state <s> ^operator <o>)
  (<s> ^analogy-method analogy-1)
  ;
  (<o> ^name imagine-task ^task <imag-t> ^fail)
 	(<imag-t> ^feature <if2>)
 	(<if2> ^fname arg1 ^fvalue <member>)
  (<s> ^class-member <member>)
  -->
  (<s> ^class-member <member> -))

(sp use-analogy*analogy-1*imagine-task*fail*terminate
  (goal <g> ^problem-space.name use-analogy ^state <s> ^operator <o>)
  (<s> ^analogy-method analogy-1)
  ;
  (<o> ^name imagine-task ^fail)
  -->
  (<g> ^operator <o> @))


;;; For an ^action, copy it onto the state and terminate the operator

(sp use-analogy*analogy-1*imagine-task*action*terminate
  (goal <g> ^problem-space.name use-analogy ^state <s> ^operator <o>)
  (<s> ^analogy-method analogy-1)
  ;
  (<o> ^name imagine-task ^action <a>)
  -->
  (<s> ^imagined-action <a>)
  (<g> ^operator <o> @))


;;; -- Making the substitution -- (step 5)

;;; We now have an ^imagined-action on the state.  We turn this into a
;;; ^analogised-action by substituting Y for X everywhere it occurs.  It's
;;; a bit ad-hoc, but we'll do this by means of a second-level copy:

;;; First, set up a blank analogised-action

(sp use-analogy*analogy-1*substitute*analogised-action
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^analogy-method analogy-1)
  ;
  (<s> ^imagined-action <a>)
  -->
  (<s> ^analogised-action <aa>))


;;; Then for each attribute, if the value is different to the ^class-member
;;; then simply copy it, but if it's the same then substitute the original
;;; object

(sp use-analogy*analogy-1*substitute*copy
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^analogy-method analogy-1)
  ;
  (<s> ^imagined-action <a> ^analogised-action <aa> ^class-member <member>)
  (<a> ^<attr> {<val> <> <member>})
  -->
  (<aa> ^<attr> <val>))

(sp use-analogy*analogy-1*substitute*substitute
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^analogy-method analogy-1)
  ;
  (<s> ^imagined-action <a> ^analogised-action <aa> ^class-member <member>)
  (<a> ^<attr> <member>)
  ;
  (<s> ^task-state.task <t>)
	(<t> ^feature <f2>)
	(<f2> ^fname arg1 ^fvalue <obj>)
  -->
  (<aa> ^<attr> <obj>))


;;; -- Finally, return the analogised-action to the super-state -- (step 6)

;;; These rules acknowledge that there is a problem with the timing
;;; of chunking if we return the action before it is fully built.

(sp use-analogy*analogy-1*recommend-analogised-action*ready
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^analogy-method analogy-1)
  ;
  (<s> ^analogised-action <aa>)
  (<aa> ^<attr> <val>)
  -->
  (<s> ^recommend ready))

(sp use-analogy*analogy-1*recommend-analogised-action*recommend
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^analogy-method analogy-1)
  ;
  (<s> ^task-state <ts> ^analogised-action <aa> ^recommend ready)
  -->
  (<ts> ^action <aa>))


;;; Now some monitoring rules to follow the progress of the analogy method

(sp use-analogy*analogy-1*monitor*state*simple-attribute
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^{<attr> << analogy-method object-class class-member >> } <val>)
  -->
  (write (crlf) |Use-analogy state has | <attr> | = | <val>))

(sp use-analogy*analogy-1*monitor*state*action
  (goal <g> ^problem-space.name use-analogy ^state <s>)
  (<s> ^{<action> << imagined-action analogised-action >> } <a>)
  (<a> ^movement <move> ^object <obj>)
  -->
  (write (crlf) |Use-analogy state has | <action> |: | <move> | | <obj>))


;;; ======================================================================
;;; IMAGINE-TASK SPACE
;;; ======================================================================

;;; The implementation space for the imagine-task operator, which is of
;;; course triggered by the Operator No-Change, is made to look in crucial
;;; respects much like the top, Perform space, so that any immediate
;;; knowledge we have about doing the task can apply (in imagination).


;;; -- Initialise --

(sp imagine-task*initialise
  (goal <g> ^object <sg> ^impasse no-change ^attribute operator)
  (<sg> ^operator (<so> ^name imagine-task))
  -->
  (<g> ^problem-space <p> ^state <s>)
  (<p> ^name perform ^type imaginary ^alias imagine-task)
  (<s> ^superoperator <so>))


;;; Copy the task onto the state

(sp imagine-task*initialise*state
  (goal <g> ^problem-space.alias imagine-task ^state <s>)
  (<s> ^superoperator.task <t>)
  -->
  (<s> ^task <t>))


;;; -- Imagine doing the task --

;;; At this point, any knowledge about how to do the task will apply. 
;;; If we succeed, an ^action will appear on the state, so we simply copy
;;; it onto the operator as a way of returning the result.

(sp imagine-task*return*action
  (goal <g> ^problem-space.alias imagine-task ^state <s>)
  (<s> ^action <a> ^superoperator <so>)
  -->
  (<so> ^action <a>))


;;; ======================================================================
;;; IMAGINE-TASK-FAIL SPACE
;;; ======================================================================


;;; If we *fail* to imagine doing the task, then we will impasse out of
;;; the Imagine-Task space, through the Action-Proposal space, and into
;;; what would be a recursive call on Use-Analogy.  We need to catch that
;;; and mark a ^fail on the (super-super-) superoperator.

;;; It's a fine point as to whether or not the failure should be chunked.
;;; For now we don't, probably more interesting not to.

;;; It would be cleaner Soar practice to set up an initial problem space and
;;; and state, at least, before passing back the ^fail.  But this is the only
;;; think we ever do in this space, and what the heck, let's keep it simple!

(sp imagine-task-fail*mark-fail
  (goal <g> ^object <sg> ^impasse no-change ^attribute operator ^quiescence t)
  (<sg> ^operator.name use-analogy ^object <ssg>)
  (<ssg> ^problem-space.alias imagine-task ^state.superoperator <ssso>)
  -->
  (<ssso> ^fail fail))

(sp imagine-task-fail*monitor
  (goal <g> ^object <sg> ^impasse no-change ^attribute operator)
  (<sg> ^operator.name use-analogy ^object <ssg>)
  (<ssg> ^problem-space.alias imagine-task)
  -->
  (write (crlf) |Fail: we don't know how to do the imagined task|))

;;; ======================================================================
;;; EOF
;;; ======================================================================
