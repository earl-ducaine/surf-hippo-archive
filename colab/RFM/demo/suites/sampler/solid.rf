;         Attribute-centered and object-centered presentation of
;                  solid-geometry knowledge in RELFUN
;                             Harold Boley                             Mar 1991

; Functions computing diagonals, areas, curfaces, and volumes of cubes, blocks,
; cylinders, cones, and truncones for symbolic (direct) or numeric ("@") use:

(ft (diagonal (cube _x)) `(* _x (sqrt 3)))
(ft (area (cube _x)) `(* 6 (expt _x 2)))
(ft (volume (cube _x)) `(expt _x 3))

(ft (diagonal (block _x _y _z)) `(pyth _x _y _z))
(ft (area (block _x _y _z)) `(* 2 (+ (* _x _y) (* _x _z) (* _y _z))))
(ft (volume (block _x _y _z)) `(* _x _y _z))

(ft (area (cylinder _r _h)) `(* 2 (pi) _r (+ _r _h)))
(ft (curface (cylinder _r _h)) `(* 2 (pi) _r _h))
(ft (volume (cylinder _r _h)) `(* (pi) (expt _r 2) _h))

(ft (area (cone _r _h)) `(* (pi) _r (+ _r (pyth _r _h))))
(ft (curface (cone _r _h)) `(* (pi) _r (pyth _r _h)))
(ft (volume (cone _r _h)) `(* (/ (pi) 3) (expt _r 2) _h))

(ft (area (truncone _r1 _r2 _h))
    `(+ @(curface `(truncone _r1 _r2 _h))
        (* (pi) (expt _r1 2))
        (* (pi) (expt _r2 2))))
(ft (curface (truncone _r1 _r2 _h)) `(* (pi) (pyth (- _r1 _r2) _h) (+ _r1 _r2)))
(ft (volume (truncone _r1 _r2 _h))
    `(* (/ (pi) 3) (+ (expt _r1 2) (* _r1 _r2) (expt _r2 2)) _h))

(ft (area (sphere _r)) `(* 4 (pi) (expt _r 2)))
(ft (volume (sphere _r)) `(* (/ 4 3) (pi) (expt _r 3)))

; Some auxiliary definitions

(ft (pi) 3.141592653589793) ; a parameterless function used as a global constant
(ft (pyth _a _b) (sqrt (+ (expt _a 2) (expt _b 2))))
(ft (pyth _a _b _c) (sqrt (+ (expt _a 2) (expt _b 2) (expt _c 2))))

; Sample listings and calls:

; rfi> l curface                      ; Attribute-centered listing,
; (ft (curface (cylinder _r _h))      ; comparable to a PROLOG procedure
;     `(* 2 (pi) _r _h) )             ; curface/1 or, to a polymorphic ML
; (ft (curface (cone _r _h))          ; function, defined for cylinder, cone
;     `(* (pi) _r (pyth _r _h)) )     ; and truncone terms
; (ft (curface (truncone _r1 _r2 _h))
;     `(* (pi) (pyth (- _r1 _r2) _h) (+ _r1 _r2)) )

; rfi> l (_att (cylinder | _par))     ; Object-centered listing,
; (ft (area (cylinder _r _h))         ; comparable to a LISP property list
;     `(* 2 (pi) _r (+ _r _h)) )      ; [(cylinder _r _h)
; (ft (curface (cylinder _r _h))      ;   < area     `(* 2 (pi) _r (+ _r _h)) >
;     `(* 2 (pi) _r _h) )             ;   < curface  `(* 2 (pi) _r _h) >
; (ft (volume (cylinder _r _h))       ;   < volume   `(* (pi) (expt _r 2) _h) >
;     `(* (pi) (expt _r 2) _h) )      ; of a PARAMETERIZED obj (cylinder _r _h)

; rfi> (area `(cylinder 3 8))         ; symbolic result obtained by direct call
; (* 2 (pi) 3 (+ 3 8))
; rfi> @(area `(cylinder 3 8))        ; numeric result obtained via "@" (ecal)
; 207.34512
