

;**********************************************************************************
;**********************************************************************************
;                      T A X O N
;       Terminological Reasoning with Concrete Domains
;**********************************************************************************
;**********************************************************************************
; DFKI Kaiserslautern, Project ARC-TEC
; C-TEC - the Knowledge Compilation Group
; TAXON: the terminological subsystem of COLAB, a hybrid knowledge representation,
;        reasoning and compilation shell
; The TAXON people: A. Abecker, D. Drollinger, Ph. Hanschke
;**********************************************************************************
 

;**********************************************************************************
; ROLEs denote binary relations between objects of the abstract domain of 
;       discourse

(role has-child   
      matriculation-department 
      is-married-to)

;**********************************************************************************
; ATTRibutes denote functional roles

(attr age matr-nr salary boss working-hours has-father has-mother
      has-physical-father has-physical-mother
      department-of-employment name colour-of-hair colour-of-dress)

;**********************************************************************************
; PRIMitive concepts are not further explained primitives of description
; the denote sets of objects of the abstract domain

(prim string date)

;**********************************************************************************
; ro identifies the concrete DOMAin of real numbers with comparison operators
; and boolean connectives

(doma ro edom-real-ord)

;**********************************************************************************
; CPRED declares predicates of the concrete domain that may be used
;       in concept descriptions later on

(cpred ro (ro (x) (= x x)))        ; x is a real number
(cpred >16 (ro (x) (> x 16)))      ; x is greater than 16
(cpred >18 (ro (x) (> x 18)))
(cpred <=300 (ro (x) (<= x 300)))
(cpred >=1000 (ro (x) (>= x 1000)))

;**********************************************************************************
; CFAM defines a family of pairwise disjoint primitive concepts
;      this is a CLOSED family, that is ALL members are known

(cfam being (male-human female-human animal thing idea))

;**********************************************************************************
; Subsets of CLOSED families may have an own name
;       they are called SFAMs (subfamilies)

(sfam human being (male-human female-human))


;**********************************************************************************
; OFAM defines an OPEN family of pairwise disjoint primitive concepts,
;      that is, the family may contain other members that are 
;      not yet known


(ofam department (computer-science sports philosophy))

;**********************************************************************************
; CONCepts are compound using previously defined things and the
;          concept forming operators of the concept description language
; they denote sets of objects of the abstract domain of discourse


(conc man (and male-human
               (some (age) >16)))

(conc woman (and female-human
                 (some (age) >16))) 

(conc fashionable-woman (and woman
                             (agree (colour-of-hair) (colour-of-dress))))

(conc person (and (or male-human
                      female-human)
                  (some (name) string)
                  (some (age) >18)))

(conc parent (and human
                  (some (has-child) human)))

(conc mother (and female-human
                  parent))

(conc father (and male-human
                  parent))

(conc father-of-sons (and father
                          (some (has-child) male-human)))

(conc father-of-daughters (and father
                          (some (has-child) female-human)))

(conc father-of-sons-and-daughters (and father
                                        (some (has-child) male-human)
                                        (some (has-child) female-human)))

(conc father-of-sons-only (and father
                               (forall (has-child) male-human)))

(conc father-of-daughters-only (and father
                                    (forall (has-child) female-human)))

(conc child (and human
                 (some (has-father) human)
                 (some (has-mother) human)))

(conc stepchild (and child
                     (or (disagree (has-father) (has-physical-father))
                         (disagree (has-mother) (has-physical-mother)))))

(conc grandparent (and parent
                       (some (has-child) parent)))

(conc grandfather (and male-human
                       grandparent))

(conc grandmother (and female-human
                       grandparent))


(conc husband (and man
                   (some (is-married-to) woman)))


(conc employee   (and person
                      (some (salary) ro)
                      (some (boss) person)))

(conc university-employee (and employee
                               (some (department-of-employment) department)))

(conc student (and person
                   (some (matr-nr) ro)
                   (some (matriculation-department) department)))

(conc working-student (and student
                           employee))

(conc poor-student (and working-student
                        (forall (salary) >=1000)))

(conc part-time-student (and working-student
                             (forall (salary) <=300)))
(conc happy-worker (and employee
                        (agree () (boss))))



(apred in-love 2)
(role relative one two)

(conc clan (some (relative) human))

(conc two-clans (and (some (one) clan)
                     (some (two) clan)))

(conc related-clans (and two-clans
                         (some (one relative) (two relative) in-love)))





